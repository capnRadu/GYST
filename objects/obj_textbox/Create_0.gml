/// @description Variables

// Input
confirm_key = vk_space;
up_key = vk_up;
down_key = vk_down;
max_input_delay = 5;
input_delay = max_input_delay;

// Position
margin = 16;
padding = 8;
width = display_get_gui_width() - margin*2;
height = sprite_height;

x = (display_get_gui_width() - width) / 2;
y = display_get_gui_height() - height - margin;

// Text
text_font = font_text;
text_color = c_white;
text_speed = 0.6;
text_x = padding;
text_y = padding;
text_width = width - padding * 2;

// Portrait
portrait_x = padding;
portrait_y = padding;

// Option
option_x = padding;
option_y = padding * -6;
option_spacing = 50;
option_selection_indent = 24; 
option_width = 300;
option_height = 40; 
option_text_x = 10;
option_text_color = c_white;

// Private properties
actions = [];
current_action = -1;

text = "";
text_progress = 0;
text_length = 0;

portrait_sprite = -1;
portrait_width = sprite_get_width(spr_portrait);
portrait_height = sprite_get_height(spr_portrait);
portrait_side = PORTRAIT_SIDE.LEFT;

enum PORTRAIT_SIDE {
	LEFT,
	RIGHT
}

options = [];
current_option = 0;
option_count = 0;

/// Methods

// Start a converstaion
setTopic = function(topic) {
	actions = global.topics[$ topic];
	current_action = -1;
	next();
}

// Move to the next action, or close the textbox if out of actions
next = function() {
	current_action++;
	if (current_action >= array_length(actions)) {
		instance_destroy();	
	}
	else {
		actions[current_action].act(id);
	}
}

// Set the text that should be typed out
setText = function(newText) {
	text = newText;
	text_length = string_length(newText);
	text_progress = 0;
}